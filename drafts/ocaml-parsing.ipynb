{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "c0d322c6",
   "metadata": {},
   "source": [
    "# Parsing with OCaml and Angstrom\n",
    "\n",
    "All opinions are my own and do not represent those of any employer, past or present.\n",
    "\n",
    "Network automation is dominated by the Python and Go programming languages.\n",
    "Python is very attractive since it has one of the lowest learning curves of any programming language, and most network automation developers and scripters started out like myself, in traditional network engineering roles.\n",
    "Python has an incredible ecosystem of tooling focused on this domain.\n",
    "Python has some serious drawbacks, too.\n",
    "It is a fat language in many respects.\n",
    "The standard library is large, it is near impossible to build small containers, and it is not terribly performant.\n",
    "I don't want to wade into the dynamic-versus-typed debate, but my opinion is that it is difficult to write \"correct\" programs in a dynamic language.\n",
    "The typing system in Python is a dumpster fire.\n",
    "Like the continuous stream of new language features, the typing committee is trying to please all masters simultaneously.\n",
    "\n",
    "Golang is an advantage in each of these situations with the added benefits of an elegant concurrency model and strongly-typed compiler.\n",
    "My main gripe with Go is that it is a strongly-opinionated language, and it is very difficult to color outside the lines.\n",
    "It lacks generics, and the current proposals leave a lot to be desired despite being huge improvements over the current situation.\n",
    "\n",
    "Functional programming is going through a bit of a renaissance, and it has become popular in fin-tech and crypto.\n",
    "Many other companies large and small are testing the waters as well.\n",
    "I would like to explore functional programming as an alternative for network automation tasks.\n",
    "I will start with a common example then deal with the language details in a future post.\n",
    "I hope showing the \"why\" inspires interest in the \"how\".\n",
    "\n",
    "## Parsing\n",
    "\n",
    "Most network automation developers are intimately familiar regular expressions and libraries like [TextFSM](https://github.com/google/textfsm).\n",
    "I never did deeply into subject, and I recently discovered the large rabbit hole that is the world of parsing.\n",
    "At a high level, parsing is simply the act of turning a string of characters into some sort of structured data.\n",
    "Parsers are used in everything from programming languages to the code that translates this markdown into HTML.\n",
    "Several tools have been created to simplify this work.\n",
    "The three broad categories are:\n",
    "\n",
    "- regular expressions\n",
    "- parser generators\n",
    "- parser combinators\n",
    "\n",
    "If you aren't familiar with regular expressions, you probably aren't interested in reading further.\n",
    "Wikipedia has great articles on each subject.\n",
    "\n",
    "### Parser generators\n",
    "\n",
    "OCaml's [Menhir](http://gallium.inria.fr/~fpottier/menhir/) library is a parser generator.\n",
    "David Beazely has [spoken](https://www.youtube.com/watch?v=zJ9z6Ge-vXs) and [written](http://www.dabeaz.com/ply/ply.html) about the subject using Python.\n",
    "Parser generators perform a task called lexicographical analysis that converts a stream of characters into a stream of \"tokens\".\n",
    "These tokens are assembled in the parsing step to build an abstract syntax tree (AST).\n",
    "Parser generators work best for well-defined grammars.\n",
    "Think programming languages, JSON, YANG, YAML, etc.\n",
    "These are streams of characters that throw errors when we hit unexpected input.\n",
    "\n",
    "### Parser combinators\n",
    "\n",
    "Lastly, we consider parser combinators (and the real focus of this post).\n",
    "From a network automation slant, we would consider parser combinators anywhere we have written bespoke regex logic.\n",
    "`Textfsm` works great in this space, but it has a few drawbacks.\n",
    "\n",
    "- It introduces yet another DSL.\n",
    "- The DSL isn't as expressive as a programming language.\n",
    "- It is untyped (as a result of the Python backend).\n",
    "- It is not very extensible (the returned data structures are not configurable).\n",
    "\n",
    "To be clear, TextFSM is my go-to library most days.\n",
    "Parser-combinators typically support look-ahead, and it is much easier to \"throw away\" data we're not interested in.\n",
    "A good example of the limitations of the DSL is in this [template](https://github.com/networktocode/ntc-templates/blob/master/ntc_templates/templates/cisco_ios_show_vlan.textfsm).\n",
    "It is clunky to deal with repeated data in certain contexts, and what if there is no upper bound on the list length?\n",
    "\n",
    "#### Side rant\n",
    "\n",
    "It is no secret that I love to hate on YAML.\n",
    "Software developers love abstractions; we live in abstractions.\n",
    "We also tend to be a bit too lazy for our own good.\n",
    "\"Arrrgggh, I'm sick of writing regex parsers, we need to abstract this into a library!\"\n",
    "\"This library is too complicated, let's write a DSL!\"\n",
    "The next thing you know...\n",
    "![standards](https://imgs.xkcd.com/comics/standards.png)\n",
    "_Source: [Randall Munroe](https://xkcd.com/927/)_\n",
    "\n",
    "I am not saying YAML isn't useful.\n",
    "I am saying that if you work with Ansible or Kubernetes that you have probably tried to build Turing-complete logic in YAML.\n",
    "Be cognizant of the limitations of your tools, and go back to your roots when the abstractions start to fall apart.\n",
    "We try to make tools accessible and easy to use for those around us, but it is difficult to replicate the expressiveness of a programming language.\n",
    "\n",
    "`</rant>`\n",
    "\n",
    "## Angstrom\n",
    "\n",
    "Below, I am using OCaml's [Angstrom](https://github.com/inhabitedtype/angstrom) library.\n",
    "The complete source is available in this [gist](https://gist.github.com/dgjustice/07e57185422b8125164bfd16d3612dfc).\n",
    "Let's consider this [raw string](https://github.com/networktocode/ntc-templates/blob/master/tests/cisco_ios/show_vlan/cisco_ios_show_vlan.raw) from Cisco IOS from the `show vlan` command:\n",
    "\n",
    "### Input\n",
    "\n",
    "```ocaml\n",
    "let vlan_raw =\n",
    "  \"VLAN Name                             Status    Ports\n",
    "---- -------------------------------- --------- -------------------------------\n",
    "1    default                          active    Gi0/1\n",
    "10   Management                       active\n",
    "50   VLan50                           active    Fa0/1, Fa0/2, Fa0/3, Fa0/4, Fa0/5, Fa0/6, Fa0/7, Fa0/8, Fa0/9\n",
    "                                                Fa0/10, Fa0/11, Fa0/12\n",
    "60   VLan60                           active    Fa0/13, Fa0/14, Fa0/15, Fa0/16, Fa0/17, Fa0/18, Fa0/19, Fa0/20\n",
    "                                                Fa0/21, Fa0/22, Fa0/23, Fa0/24\n",
    "1002 fddi-default                     act/unsup\n",
    "1003 token-ring-default               act/unsup\n",
    "1004 fddinet-default                  act/unsup\n",
    "1005 trnet-default                    act/unsup\n",
    "VLAN Type  SAID       MTU   Parent RingNo BridgeNo Stp  BrdgMode Trans1 Trans2\n",
    "---- ----- ---------- ----- ------ ------ -------- ---- -------- ------ ------\n",
    "1    enet  100001     1500  -      -      -        -    -        0      0\n",
    "10   enet  100010     1500  -      -      -        -    -        0      0\n",
    "50   enet  100050     1500  -      -      -        -    -        0      0\n",
    "60   enet  100060     1500  -      -      -        -    -        0      0\n",
    "1002 fddi  101002     1500  -      -      -        -    -        0      0\n",
    "1003 tr    101003     1500  -      -      -        -    srb      0      0\n",
    "1004 fdnet 101004     1500  -      -      -        ieee -        0      0\n",
    "1005 trnet 101005     1500  -      -      -        ibm  -        0      0\n",
    "Remote SPAN VLANs\n",
    "------------------------------------------------------------------------------\n",
    "Primary Secondary Type              Ports\n",
    "------- --------- ----------------- ------------------------------------------\"\n",
    "\n",
    "\n",
    "```\n",
    "\n",
    "### Data structures\n",
    "\n",
    "We wish to capture the two tables and transform them to structured data.\n",
    "OCaml has record types.\n",
    "These _look_ like annotated Python `dict` types, but they are closer conceptually to named tuples.\n",
    "The primary difference is that these types are not keyed mappings.\n",
    "\n",
    "```ocaml\n",
    "type vlan = {\n",
    "  vlan_id : int;\n",
    "  name : string;\n",
    "  status : string;\n",
    "  ports : string list;\n",
    "}\n",
    "\n",
    "type vlan_stat = {\n",
    "  vlan_id : int;\n",
    "  vl_type : string;\n",
    "  said : string;\n",
    "  mtu : int;\n",
    "  parent : string;\n",
    "  ring_no : string;\n",
    "  bridge_no : string;\n",
    "  stp : string;\n",
    "  bridge_mode : string;\n",
    "  trans1 : int;\n",
    "  trans2 : int;\n",
    "}\n",
    "```\n",
    "\n",
    "### Rules\n",
    "\n",
    "Next, we need some parsing rules.\n",
    "Your first thought might be, \"Why not use regex?\".\n",
    "The reason is that this parser-combinator lexes a character at a time.\n",
    "We can match on strings, but this is simply a recursive implementation of the character processor.\n",
    "I followed the naming convention in Angstrom of using a `1` to indicate that a rule requires at least one match.\n",
    "\n",
    "Speaking of empty matches... this is the primary \"gotcha\" that I ran into using Angstrom.\n",
    "If your parser hangs and starts chewing through vast amounts of memory and CPU, double check your rules.\n",
    "Angstrom doesn't handle arbitrary conditions well, and happily recurses itself into a corner if you mess up.\n",
    "Be mindful of cases where a string **must** exist or *may* exist.\n",
    "Try not to get hung up on the syntax; I will explain the language in much more detail in another post.\n",
    "\n",
    "```ocaml\n",
    "let is_whitespace = function '\\x20' | '\\x09' -> true | _ -> false\n",
    "\n",
    "let whitespace = take_while is_whitespace\n",
    "\n",
    "let whitespace1 = take_while1 is_whitespace\n",
    "\n",
    "let is_newline = function '\\x0a' | '\\x0d' -> true | _ -> false\n",
    "\n",
    "let newline = take_while is_newline\n",
    "\n",
    "let newline1 = take_while1 is_newline\n",
    "\n",
    "let is_digit = function '0' .. '9' -> true | _ -> false\n",
    "\n",
    "let digit = take_while is_digit\n",
    "\n",
    "let digit1 = take_while is_digit\n",
    "\n",
    "let is_alpha = function\n",
    "  | '_' | '-' | 'a' .. 'z' | 'A' .. 'Z' -> true\n",
    "  | _ -> false\n",
    "\n",
    "let is_iface_char = function '/' | '.' -> true | _ -> false\n",
    "\n",
    "let is_ident c = is_alpha c || is_digit c || is_iface_char c\n",
    "\n",
    "let ident = take_while is_ident\n",
    "\n",
    "let ident1 = take_while1 is_ident\n",
    "```\n",
    "\n",
    "### Handwaving and magic\n",
    "\n",
    "If you are familiar with ML-family languages, this will look a lot less magical.\n",
    "If you come from an imperative language, this following looks like pure chicken scratch.\n",
    "These operators are the \"combinator\" part of \"parser-combinator\".\n",
    "We can write a bunch of rules and glue them together in interesting ways.\n",
    "Programmers know about modularity, this is all about modularity!\n",
    "\n",
    "- `*>` is an operator that throws away the preceding output and returns the next expression\n",
    "- `<*` does just the opposite\n",
    "- `>>=` binds two functions together, chaining the results together\n",
    "- `variable |> int_of_string` is a way of applying a type cast to a result\n",
    "- `return` binds the results together given our data structures at the beginning\n",
    "\n",
    "Hopefully, the flow will make some sense.\n",
    "These build more complex parsers from the simple rules above.\n",
    "\n",
    "```ocaml\n",
    "let vl_header =\n",
    "  Angstrom.string \"VLAN\" *> take_while1 is_whitespace *> string_ci \"name\"\n",
    "  *> take_while1 is_whitespace *> string_ci \"status\"\n",
    "  *> take_while1 is_whitespace *> string_ci \"ports\" *> take_while is_whitespace\n",
    "  *> newline\n",
    "\n",
    "let sep_row = Angstrom.string \"--\" *> take_till is_newline *> skip_many newline1\n",
    "\n",
    "let vl_stat_header =\n",
    "  Angstrom.string \"VLAN\" *> whitespace *> string_ci \"type\" *> whitespace\n",
    "  *> string_ci \"said\" *> whitespace *> string_ci \"mtu\" *> take_till is_newline\n",
    "  *> newline\n",
    "\n",
    "let iface_list = sep_by (string \", \") ident1 <* newline1\n",
    "\n",
    "(* VLAN Name Status Ports *)\n",
    "let vl_data_row =\n",
    "  digit1 <* whitespace1 >>= fun vid ->\n",
    "  ident1 <* whitespace1 >>= fun name ->\n",
    "  ident1 <* whitespace >>= fun status ->\n",
    "  iface_list >>= fun ifaces ->\n",
    "  many (whitespace1 *> iface_list) >>= fun add_ifaces ->\n",
    "  return\n",
    "    {\n",
    "      vlan_id = vid |> int_of_string;\n",
    "      name;\n",
    "      status;\n",
    "      ports = List.append ifaces (List.flatten add_ifaces);\n",
    "    }\n",
    "\n",
    "(* VLAN Type SAID MTU Parent RingNo BridgeNo Stp BrdgMode Trans1 Trans2 *)\n",
    "let vl_stats_row =\n",
    "  digit1 <* whitespace1 >>= fun vid ->\n",
    "  ident1 <* whitespace1 >>= fun vtype ->\n",
    "  ident1 <* whitespace1 >>= fun said ->\n",
    "  ident1 <* whitespace1 >>= fun mtu ->\n",
    "  ident1 <* whitespace1 >>= fun parent ->\n",
    "  ident1 <* whitespace1 >>= fun ringno ->\n",
    "  ident1 <* whitespace1 >>= fun brno ->\n",
    "  ident1 <* whitespace1 >>= fun stp ->\n",
    "  ident1 <* whitespace1 >>= fun brmode ->\n",
    "  ident1 <* whitespace1 >>= fun t1 ->\n",
    "  ident1 <* whitespace1 <* skip_many newline1 >>= fun t2 ->\n",
    "  return\n",
    "    {\n",
    "      vlan_id = vid |> int_of_string;\n",
    "      vl_type = vtype;\n",
    "      said;\n",
    "      mtu = mtu |> int_of_string;\n",
    "      parent;\n",
    "      ring_no = ringno;\n",
    "      bridge_no = brno;\n",
    "      stp;\n",
    "      bridge_mode = brmode;\n",
    "      trans1 = t1 |> int_of_string;\n",
    "      trans2 = t2 |> int_of_string;\n",
    "    }\n",
    "\n",
    "let vlan_info = vl_header *> sep_row *> many vl_data_row <* skip_many newline1\n",
    "\n",
    "let vlan_stats =\n",
    "  vl_stat_header *> sep_row *> many vl_stats_row <* skip_many any_char\n",
    "```\n",
    "\n",
    "### Tie it all together\n",
    "\n",
    "The last expression is the final piece of glue to wrap up all of the above work.\n",
    "It returns a tuple of lists of `vlan` and `vlan_stat` defined in the beginning.\n",
    "\n",
    "``` ocaml\n",
    "let p =\n",
    "  match\n",
    "    Angstrom.parse_string ~consume:All\n",
    "      ( vlan_info >>= fun vli ->\n",
    "        vlan_stats >>= fun vls -> return (vli, vls) )\n",
    "      vlan_raw\n",
    "  with\n",
    "  | Ok v -> v\n",
    "  | Error msg -> failwith msg\n",
    "```\n",
    "\n",
    "### And produce a result\n",
    "\n",
    "At long last, Angstrom \"magic\" produces a useful result!\n",
    "\n",
    "```ocaml\n",
    "utop # Lib.Vlan.p;;\n",
    "- : Lib.Vlan.vlan list * Lib.Vlan.vlan_stat list =\n",
    "([{Lib.Vlan.vlan_id = 1; name = \"default\"; status = \"active\";\n",
    "   ports = [\"Gi0/1\"]};\n",
    "  {Lib.Vlan.vlan_id = 10; name = \"Management\"; status = \"active\"; ports = []};\n",
    "  {Lib.Vlan.vlan_id = 50; name = \"VLan50\"; status = \"active\";\n",
    "   ports =\n",
    "    [\"Fa0/1\"; \"Fa0/2\"; \"Fa0/3\"; \"Fa0/4\"; \"Fa0/5\"; \"Fa0/6\"; \"Fa0/7\"; \"Fa0/8\";\n",
    "     \"Fa0/9\"; \"Fa0/10\"; \"Fa0/11\"; \"Fa0/12\"]};\n",
    "  {Lib.Vlan.vlan_id = 60; name = \"VLan60\"; status = \"active\";\n",
    "   ports =\n",
    "    [\"Fa0/13\"; \"Fa0/14\"; \"Fa0/15\"; \"Fa0/16\"; \"Fa0/17\"; \"Fa0/18\"; \"Fa0/19\";\n",
    "     \"Fa0/20\"; \"Fa0/21\"; \"Fa0/22\"; \"Fa0/23\"; \"Fa0/24\"]};\n",
    "  {Lib.Vlan.vlan_id = 1002; name = \"fddi-default\"; status = \"act/unsup\";\n",
    "   ports = []};\n",
    "  {Lib.Vlan.vlan_id = 1003; name = \"token-ring-default\"; status = \"act/unsup\";\n",
    "   ports = []};\n",
    "  {Lib.Vlan.vlan_id = 1004; name = \"fddinet-default\"; status = \"act/unsup\";\n",
    "   ports = []};\n",
    "  {Lib.Vlan.vlan_id = 1005; name = \"trnet-default\"; status = \"act/unsup\";\n",
    "   ports = []}],\n",
    " [{Lib.Vlan.vlan_id = 1; vl_type = \"enet\"; said = \"100001\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\"; bridge_mode = \"-\";\n",
    "   trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 10; vl_type = \"enet\"; said = \"100010\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\"; bridge_mode = \"-\";\n",
    "   trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 50; vl_type = \"enet\"; said = \"100050\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\"; bridge_mode = \"-\";\n",
    "   trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 60; vl_type = \"enet\"; said = \"100060\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\"; bridge_mode = \"-\";\n",
    "   trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 1002; vl_type = \"fddi\"; said = \"101002\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\"; bridge_mode = \"-\";\n",
    "   trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 1003; vl_type = \"tr\"; said = \"101003\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"-\";\n",
    "   bridge_mode = \"srb\"; trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 1004; vl_type = \"fdnet\"; said = \"101004\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"ieee\";\n",
    "   bridge_mode = \"-\"; trans1 = 0; trans2 = 0};\n",
    "  {Lib.Vlan.vlan_id = 1005; vl_type = \"trnet\"; said = \"101005\"; mtu = 1500;\n",
    "   parent = \"-\"; ring_no = \"-\"; bridge_no = \"-\"; stp = \"ibm\";\n",
    "   bridge_mode = \"-\"; trans1 = 0; trans2 = 0}])\n",
    "```\n",
    "\n",
    "There is a lot more that could be done with this, especially around data validation.\n",
    "So far, the only thing I check for is that certain values are integers.\n",
    "We could wrap those `\"-\"` in `Option`, `STP` in a sum type, etc.\n",
    "This is where we start pulling ahead of Python in terms of type safety and data integrity.\n",
    "\n",
    "## Going forward\n",
    "\n",
    "Do I plan to rewrite the `ntc-templates` library in OCaml?\n",
    "No!\n",
    "It does a splendid job, and has fantastic support.\n",
    "\n",
    "I would love to make a case for using functional languages in automation tasks, but it's not practical yet.\n",
    "Ideas don't pay my bills, nor do rants about software quality and testing.\n",
    "First and foremost, software **must** be usable.\n",
    "Haskell is a wonderful language in many aspects, but it is so hard to do \"real\" work with it.\n",
    "We don't live in a pure data utopia, and I'm no postdoc getting paid by a university.\n",
    "I will go into more details as to \"why OCaml?\" in another post, but I wanted to whet your appetite something practical and useful."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
